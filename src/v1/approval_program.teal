#pragma version 2

// check if the app is being created
int 0
txn ApplicationID
==
bz not_creation

// store creator address
byte "Creator"
txn Sender
app_global_put

// verify that 5 arguments passed
txn NumAppArgs
int 5
==
bz failed

// store the start date
byte "StartDate"
txna ApplicationArgs 0
btoi
app_global_put

// store the end date
byte "EndDate"
txna ApplicationArgs 1
btoi
app_global_put

// store the id of bond
byte "AssetID"
txna ApplicationArgs 2
btoi
app_global_put

// store the cost of bond
byte "BondCost"
txna ApplicationArgs 3
btoi
app_global_put

// store the principal of bond
byte "BondPrincipal"
txna ApplicationArgs 4
btoi
app_global_put

// return true as approval
b success

not_creation:
// check if this is NoOp transaction
int NoOp
txn OnCompletion
==
bz failed

// if here then application call
// verify there are three transactions in atomic transfer
// 0. call to this contract
// 1. transfer of algos 
// 2. transfer of bond
global GroupSize
int 3
==
gtxn 1 TypeEnum
int pay
==
gtxn 2 TypeEnum
int axfer
==
&&
&&
bz failed

// check if buying bond
txna ApplicationArgs 0
byte "buy"
==
bnz buy

// check if claiming principal
txna ApplicationArgs 0
byte "principal" 
==
bnz principal
err

buy:
// verify buy:
// a) before start date
// b) creator is receiving algos
// c) sender receiving bond
// d) transfering bond
// e) paying BondCost (per bond)
global LatestTimestamp
byte "StartDate"
app_global_get
<
byte "Creator"
app_global_get
gtxn 1 Receiver
==
&&
txn Sender
gtxn 2 AssetReceiver
==
&&
byte "AssetID"
app_global_get
gtxn 2 XferAsset
==
&&
gtxn 1 Amount
gtxn 2 AssetAmount
/
byte "BondCost"
app_global_get
==
&&
bnz success
b failed

principal:
// verfiy sell:
// a) after end date
// b) creator is receiving bond
// c) sender receiving algos
// d) transfering bond
// e) paying BondPrincipal (per bond)
global LatestTimestamp
byte "EndDate"
app_global_get
>=
byte "Creator"
app_global_get
gtxn 2 AssetReceiver
==
&&
txn Sender
gtxn 1 Receiver
==
&&
byte "AssetID"
app_global_get
gtxn 2 XferAsset
==
&&
gtxn 1 Amount
gtxn 2 AssetAmount
/
byte "BondPrincipal"
app_global_get
==
&&
bz failed

// return true as success
success:
int 1
return

// return false as failure
failed:
int 0
return
